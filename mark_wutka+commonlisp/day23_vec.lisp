(load "mwlib.lisp")

;;; After finishing the day23 solution I realized that instead of
;;; keeping a circular list, I could dispense with the circular list
;;; and just keep a vector of next indices, so the number that comes
;;; after 1 in the list is stored at nexts[1]
;;; I also changed destination and insert-cups to not take a list of
;;; new cups, which significantly reduced the object creation and cut
;;; the execution time by more than half.

(defun destination (rc1 rc2 rc3 num max)
  (if (< num 1) (destination rc1 rc2 rc3 max max)      
      (if (not (or (= num rc1) (= num rc2) (= num rc3))) num
	  (destination rc1 rc2 rc3 (1- num) max))))

;;; Insert the new cups after the given destination number
(defun insert-cups (first-new last-new dest nexts)
    ;;; The last cup in the inserted cups should point to the cell
    ;;; that dest currently points to
  (setf (aref nexts last-new) (aref nexts dest))
    ;;; Dest should now point to the first cell in new-cups
  (setf (aref nexts dest) first-new))

(defun play-round (nexts curr-cup max-cup)
  ;;; Get the reference to the 3 cups to move
  (let* ((next-1 (aref nexts curr-cup))
	 (next-2 (aref nexts next-1))
	 (next-3 (aref nexts next-2)))    
;;; Remove the 3 cups from the list by making curr-cup point
 ;;; to what next-3 pointed to
    (setf (aref nexts curr-cup) (aref nexts next-3))

    ;;; Insert the removed cups back at the designated index
    (insert-cups next-1 next-3 (destination next-1 next-2 next-3
					(1- curr-cup)
					max-cup)
		 nexts)
    ;;; Shift over to the next cup
    (aref nexts curr-cup)))

;;; Keep playing rounds until the rounds counter hits 0
(defun play-rounds (nexts curr-cup rounds max-cup)
  (if (< rounds 1) curr-cup
      (play-rounds nexts (play-round nexts curr-cup max-cup) (1- rounds) max-cup)))

;;; Creates a vector of next indexes by mapping over the list and the lest
;;; rotated by one position (put the first element on the end)
(defun make-nexts (number-list)
  (let ((nexts (make-array (1+ (length number-list)))))
    (mapcar (lambda (a b) (setf (aref nexts a) b))
	    number-list (append (cdr number-list) (list (car number-list))))
    nexts))

;;; Returns a chain of num items starting at start
(defun get-chain (start num nexts)
  (if (= num 0) '()
      (cons start (get-chain (aref nexts start) (1- num) nexts))))

(defun day23a ()
  (let* ((start-list '(1 2 3 4 8 7 5 9 6))
	 (nexts (make-nexts start-list)))
    (play-rounds nexts (car start-list) 100 9)
    (format t "宁アㄣ潋ㄧ弭汨衢铄趔┅┅ㄤ彐躅溽渤ī换阵轱翎麸泸遽翦翳扉篝镦铛礅弪躔麸卑鞍鞍艾蝈痨徙翳骈蝮换鏖翳翳瘐戾轭瘐扉篝戾舄è铛礅弪扉篝ㄡ痧孱Ж订ㄤ蝻ㄩ雉卑鞍鞍后翎螋暴┅铄趔磲脲铄趔铛礅弪扉篝┅痨狴蝻躅潴铄趔ㄣ狎铛礅弪扉篝卑鞍鞍鞍卑鞍鞍癌换ㄡ蝈轭溴暴痫轭趔麸翳沐祆泔铘衢铋铉爆箫麇麽铘翳换轸屙徭翦翳狒沐祆箦泔钿犷翳轸屙徭翦翳狒镱翳轵洎ㄡ痧禊＇ㄣ潋ㄧ弭汨衢铄趔┅┅